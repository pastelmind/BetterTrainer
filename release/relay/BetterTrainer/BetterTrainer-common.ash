/**
 * Common functions used by BetterTrainer
 */

script "BetterTrainer-common";
since 20.7;


/**
 * Used to set the current file name shown in debug message
 */
string __CURRENT_FILE__ = "__CURRENT_FILE__ NOT SET";

/**
 * Decorates a text message with debugging info (file name, timestamp).
 * @param   msg   Debug message to decorate
 * @return  Decorated debug message
 */
string _debug_msg(string msg) {
  string timestamp = now_to_string("HH:mm:ss.SSS z");
  return `[{__CURRENT_FILE__}][{timestamp}] {msg}`;
}

// Prints a debug message to the CLI
void _debug(string msg) {
  if (get_property("philmasterplus_BetterTrainer_DebugMode") != "") {
    print(_debug_msg(msg));
  }
}

// Prints a debug message to the CLI and aborts the script
void _error(string msg) {
  abort(_debug_msg(`ERROR: {msg}`));
}

/**
 * Sets the current file name of the script shown in debug messages.
 * @param   filename  Current file name
 */
void _set_current_file(string filename) {
  __CURRENT_FILE__ = filename;
  _debug("Current file set");
}

/**
 * Appends the string, followed by a newline, to the buffer.
 * @param text Buffer to append to
 * @param to_append String to append to the buffer
 * @return The modified buffer
 */
buffer appendln(buffer text, string to_append) {
  return text.append(to_append).append("\n");
}

/**
 * Generates one or more <script> and <link> tags that enable skill tooltips
 * @return HTML markup containing one or more <script> and <link> tags
 */
string generate_tooltip_head_tags() {
  buffer html;
  // Comment to make debugging easier
  html.appendln('\n<!-- Generated by BetterTrainer (guild.ash) -->');

  // We use Tippy (which depends on Popper)
  html.appendln('<script src="/BetterTrainer/popper.min.js"></script>');
  html.appendln('<script src="/BetterTrainer/tippy-bundle.umd.min.js"></script>');

  html.appendln('<script src="/BetterTrainer/BetterTrainer.js"></script>');
  html.appendln('<link rel="stylesheet" href="/BetterTrainer/BetterTrainer.css">');

  html.appendln('<!-- End of code by BetterTrainer (guild.ash) -->');
  return html;
}
